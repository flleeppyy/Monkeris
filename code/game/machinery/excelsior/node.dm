/obj/machinery/node
	name = "Excelsior \"Tochka\" node"
	var/shortname = "Tochka-123"
	icon = 'icons/obj/machines/excelsior/corenode/node.dmi'
	desc = "Bullet resistant transmission receiver and spreader. Reaches for signals like antennas to Haven."
	icon_state = "on"
	description_info = "Node scans tiles to get teleporter power from them, activates turrets, and reports intruders to Excelsior communications."
	description_antag = "Repairable with welding tools. Node radius can be seen with Influence Mode on KOMPAK. Connecting them to Centor and each other makes them work."
	anchored = TRUE
	density = TRUE
	circuit = /obj/item/electronics/circuitboard/excelsior_node
	health = 1200
	maxHealth = 1200
	shipside_only = TRUE
	layer = 5
	var/list/obj/machinery/linked = list()
	var/list/obj/machinery/node/neighbours = list()
	var/obj/machinery/centor/core		// we wanna know whos our dad
	var/damage_report_cooldown = FALSE

	//var/emplacement_storage = 4
	var/list/localmarkerlist = list() 	/* On destroy() or "turning off" (if disconnected from Centor's node chain) will...
											> remove the whole local list (node's) from global one (Centor interacts with it)
											- Is Feature, cut off Excelsior's "logistics" and forward bases won't work :)
										 */
	var/list/activemarkerlist = list()
	var/what_is_marker = /obj/effect/effect/excelsior_influence

	// Intruder Reporting
	var/list/intruder_list = list()
	var/report_cooldown


/*
*	Basics
*/


/obj/machinery/node/examine(mob/user, extra_description)
	if(!core)
		extra_description += "\n<b>Seems to be powered down.</b> No active Excelsior node or Centor found nearby."
	. = ..()


/obj/machinery/node/proc/make_name()
	var/list/namelist = list(
	"Zvezda",
	"Barrikada",
	"Volna",
	"Abzats",
	"Pioner",
	"Dyatel",
	"Malyutka",
	"Durak",
	"Vampir",
	"Kolobok",
	"Udav",
	"Zenit",
	"Sport",
	"Spidola",
	"Mayak",
	"Zorkiy",
	"Iskra",
	"Lider",
	"Sirius",
	"Yunost",
	"Melodiya",
	"Vega",
	"Rondo",
	"Korvet",
	"Kantata",
	"Serenada",
	"Arktur",
	"Ilga",
	"Tochka",
	"Sovet",
	"Sakhar",
	"Krona",
	"Praktik",
	"Kozyol",
	"Partisan",
	)

	var/newname = pick(namelist)
	var/cifra = rand(100, 999)	// cifra does nothing except goes to a name
	name = "Excelsior \"[newname]-[cifra]\" node"
	shortname = "[newname]-[cifra]"


/obj/machinery/node/assign_uid()	// this is for UI because UI wants a reference to obj :)
	uid = rand(1, 3000)
	for(var/obj/machinery/node/node in excelsior_nodes)
		if(node.uid == uid)	//lets compare all IDs so they dont match (if they do just reroll :3)
			assign_uid()


/obj/machinery/node/Initialize(mapload, d)
	. = ..()
	make_name()
	assign_uid()
	excelsior_nodes.Add(src)
	search_for_machines()
	search_for_nodes()
	define_influence()
	if(excelsior_centor)
		var/obj/machinery/centor/C = excelsior_centor
		C.load_network()
	update_icon()







/obj/machinery/node/Destroy()
	for(var/datum/excelsior_junction/short_road in excelsior_junctions)//Clean up connected roads
		if(short_road.first == src || short_road.second == src)
			excelsior_junctions.Remove(short_road)
			short_road.Destroy()
	cleanup_influence()
	UnregisterSignal(src, COMSIG_TURF_LEVELUPDATE)
	for(var/obj/machinery/node/noder in neighbours)
		noder.update_influence()
	. = ..()

	excelsior_nodes.Remove(src)
	for(var/obj/machinery/machine in linked)
		SEND_SIGNAL(machine, COMSIG_EX_CONNECT)
	for(var/obj/machinery/node/N in neighbours)
		N.disconnect(src, TRUE)
	if(core)
		core.load_network()







/obj/machinery/node/proc/update_influence()
	cleanup_influence()
	define_influence()





/obj/machinery/node/proc/define_influence()
	for(var/turf/selected in circlerangeturfs(src, EX_NODE_DISTANCE))
		if(!locate(/obj/effect/effect/excelsior_influence) in selected)
			var/influence_marker = new /obj/effect/effect/excelsior_influence(loc = selected, creator = src)
			if(influence_marker)
				localmarkerlist.Add(influence_marker)
		else
			continue






/obj/machinery/node/proc/cleanup_influence()
	for(var/marker in localmarkerlist)
		QDEL_NULL(marker)
	localmarkerlist = list()
	activemarkerlist = list()






/*/obj/machinery/node/proc/pick_up_emplacement(var/mob/living/carbon/human/user)
	if(emplacement_storage >= 1)
		var/obj/item/unemplacement/emplacement = /obj/item/unemplacement	// item that will then become the machinery
		user.put_in_active_hand(new emplacement)
		emplacement_storage--
															//!!!!add ability to put it back in - delete comment if done
*/




/obj/machinery/node/attackby(obj/item/I, mob/user)
	if(user.a_intent == I_HELP)
		if((QUALITY_WELDING in I.tool_qualities) && (health < maxHealth))
			if(I.use_tool(user, src, WORKTIME_LONG, QUALITY_WELDING, FAILCHANCE_EASY,  required_stat = STAT_MEC))
				health += 200
				if(health > maxHealth)
					health = maxHealth
				update_icon()
		return 1
	if (!(I.flags & NOBLUDGEON) && I.force)
		//if the turret was attacked with the intention of harming it:
		user.do_attack_animation(src)
		user.setClickCooldown(DEFAULT_ATTACK_COOLDOWN)
		take_damage(I.force * I.structure_damage_factor)

	..()

/obj/machinery/node/bullet_act(obj/item/projectile/Proj)
	var/damage = Proj.get_structure_damage()
	..()
	take_damage(damage*Proj.structure_damage_factor)

/obj/machinery/node/take_damage(amount)
	if(!damage_report_cooldown)
		talk("DAMAGED :: Node [shortname] lost integrity. ")
		damage_report_cooldown = TRUE
		spawn(1 MINUTE)
			if(src)
				damage_report_cooldown = FALSE
	if(!amount)
		return FALSE	//No damage done. Used in attackby()
	health -= amount
	if(health <= 0)
		die()
	update_icon()
	return TRUE	//Actual damage delt. Used in attackby()

/obj/machinery/node/proc/die()
	talk("DESTROYED :: [shortname] reported demolished at [get_area(src)]")
	explosion(get_turf(src), 100, 50)
	Destroy()

/obj/machinery/node/update_icon()
	overlays.Cut()
	icon_state = "on"

	if(!core)
		overlays += "off_overlay"
	if(health <= maxHealth * 0.25)
		icon_state = "damaged_heavy"
		return
	if(health <= maxHealth * 0.5)
		icon_state = "damaged_moderate"
		return
	if(health <= maxHealth * 0.75)
		icon_state = "damaged_light"
		return






/obj/machinery/node/attack_hand(mob/user)
//	. = ..()		// DONT uncomment, unless you wanna give it power consumption :)		(P.S. I DONT want that)
	to_chat(user, "Node's screen blinks for a brief moment revealing it's statistics")
	to_chat(user, "Linked nodes:")
	for(var/obj/machinery/machine in neighbours)
		to_chat(user, "[machine.name] [dist3D(src, machine)]m away")
	to_chat(user, "Current coverage is at [round(activemarkerlist.len / localmarkerlist.len * 100, 0.1)]%")
	//pick_up_emplacement(user)		// later






// Some structures need node in radius to power up and work, this is the proc that searches (e.g. emplacements)
/obj/machinery/node/proc/search_for_machines()
	for(var/obj/machinery/machine in circlerange(src, EX_NODE_DISTANCE))
		SEND_SIGNAL(machine, COMSIG_EX_CONNECT)






//Searches for other nodes EVEN BETWEEN Z LEVELS.
/obj/machinery/node/proc/search_for_nodes()
	for(var/obj/machinery/node/N in excelsior_nodes)
		if(dist3D(src, N) <= EX_NODE_DISTANCE*2 && N != src)
			connect(N, TRUE)
			N.connect(src, TRUE)
			if(N.core)
				src.spread_signal(N.core)






//	# Adds machine to either list of connected nodes or list of connected machines as specified by is_node argument
//Checks if machine is on the list before adding to avoid dupes
/obj/machinery/node/proc/connect(var/obj/machinery/M, var/is_node = FALSE)
	if(is_node)
		if(!neighbours.Find(M)) // > Connect to node
			neighbours.Add(M)
	else
		if(!linked.Find(M))		// > Connect to emplacements, for example.
			linked.Add(M)		//	- If such machinery demands Node's connection to work (sentry)






//Removes machine from list of nodes or list of machines as specifed by is_node argument
//Checks if machine is on the list before deletion
/obj/machinery/node/proc/disconnect(var/obj/machinery/M, var/is_node = FALSE)
	if(is_node)
		if(neighbours.Find(M))
			neighbours.Remove(M)
	else
		if(linked.Find(M))
			linked.Remove(M)








/obj/machinery/node/proc/spread_signal(var/center)	// 	# Nodes check if they are connected to Centor, directly or not (node chain)
	if(!center)										//	 Special case - we are trying to reset node's core
		core = null
		update_icon()
	if(core)										//	 1.	If not - connect to Centor
		return										//	 2.	Pass "core connected" status through the chain
	core = center
	update_icon()
	core.antennas_to_haven.Add(src)
	for(var/obj/machinery/node/N in neighbours)
		N.spread_signal(center)
													// NOTE: "Core+Node gameplay is defined by territorial control of excelsior

/obj/machinery/node/verb/pack()
	set name = "Pack node"
	set category = "Object"
	set src in oview(1)

	if(usr.stat || !usr.canmove || usr.restrained())
		return
	if(!is_excelsior(usr))
		to_chat(usr, "It doesn't listen to you.")
		return
	to_chat(usr, "You've started packing up the node.")
	if(do_after(usr, 2 SECONDS, src))
		new /obj/item/machinery_crate/excelsior/node(loc)
		Destroy()




									/*
									*	      Nodes speak into Excelsior comms
									*/





// # DETECTION of non-excelsior human/robot

//		- The act of yapping itself

/obj/machinery/proc/talk(message)							// the act of yapping
	var/datum/faction/F = get_faction_by_id(FACTION_EXCELSIOR)
	if(!F)
		return
	F.communicate_inanimate(src, message)



//		- The thinking behind reporting a bypasser

/obj/machinery/node/proc/intruder_alert(var/mob/living/intruder)// TODO: Move to KOMPAK logs
																// TODO: Ask Node what the human has in weapons through KPK
	if(world.time - report_cooldown >= 15 SECONDS)	// Don't report the same person twice in x seconds
		intruder_list = list()
		report_cooldown = world.time

	if(intruder_list.Find(intruder))	// We don't need the same guy reported
		return
	if(istype(intruder, /mob/living/carbon/human))
		if(intruder.stats.getPerk(PERK_VAGABOND) || intruder.name == "Unknown")
			talk("SPOTTED: Non-crew [intruder.name] spotted at [name]")
			intruder_list.Add(intruder)
			return
		talk("SPOTTED :: Human [intruder.name] spotted at [name]")
		intruder_list.Add(intruder)
		return
	if(istype(intruder, /mob/living/silicon/robot))
		talk("SPOTTED :: Robot [intruder.name] spotted at [name]")
		intruder_list += intruder
		return

	//MESSAGE
	//	//clean stuff that was reported








												/******************************
 												*		   Influence		  *
 												*******************************/

/* [?] INFLUENCE is an invisible obj called [excelsior_influence], it produces "teleporter energy" for Excelsior 	--- [ex_teleporter.dm]
		1.	NODE spawns around itself [excelsior_influence] in a radius, defined by EX_NODE_DISTANCE 				--- [_excelsior_defines.dm]
		2.	INFLUENCE checks the [turf] it stands on, if it has whitelisted turfs (floortiles & low walls), if not - it won't be eligible for power generation.
			- by design walls and space is unwelcome as "territory that rewards you"

		3. NODES connected to CENTOR (let's call it "centored" or smth), or if NODE connected to another "centored" NODE, only then does it produce teleporter energy,
			if it's cut off from the "base" (CENTOR), it doesn't turn on sentries and doesn't give telepower.
*/

/obj/effect/effect/excelsior_influence	//zone make excel energy :)			//	# Visible on Influence Mode on centor_kpk [KPK.dm].
	var/active = FALSE														// 	- To find the HUD code do either:
	var/obj/machinery/node/node												//		> Search by "process_excel_hud" in
																			//		> hud.dm [code\defines\procs][line 60~]




/obj/effect/effect/excelsior_influence/New(loc, var/obj/machinery/node/creator)		// > Code one "thinking layer" above is define_influence()
	..(loc)
	icon = null
	icon_state = null
	node = creator
	validate()
	RegisterSignal(src, COMSIG_TURF_LEVELUPDATE, PROC_REF(validate))				// # Any tile on map built/destroyed:
																					//	1.	Sends a COMSIG_TURF_LEVELUPDATE signal
																					//		To every obj standing on top of said tile
																					//	2.	It's up to obj to receive that signal
																					//	3.	Marker receives that signal >> validate()






/obj/effect/effect/excelsior_influence/Destroy()
	. = ..()
	UnregisterSignal(src, COMSIG_TURF_LEVELUPDATE)															// no phantom pain sry





/obj/effect/effect/excelsior_influence/proc/validate()	// # Checks if influence zone is "active".
    if(!node)                                           //	 It's active if...
        Destroy()
        return
    var/turf/my_turf = get_turf(src)
    for(var/type in excelsior_turf_whitelist)				//	...Stuff inside it matches whitelist 											---	[_excelsior_defines.dm]
        if(istype(my_turf, type))							//		Every obj inside whitelist allows "influence tiles" to generate excel energy.
            active = TRUE									//		We chose it to be floors and low walls. Walls are punished we hate walls.
            if(!node.activemarkerlist.Find(src))			//		That may change because of YOU, you stinky game designer, that's why the list exists.
                node.activemarkerlist.Add(src)
            return TRUE
    active = FALSE
    if(node.activemarkerlist.Find(src))
        node.activemarkerlist.Remove(src)
    return FALSE





/obj/effect/effect/excelsior_influence/Crossed(atom/movable/O)
	var/mob/living/intruder = O
	if(!intruder)
		return
	if(!istype(intruder, /mob))
		return
	if(!is_excelsior(intruder))												// 	1.	If EXCELSIOR = STOP
		if(!intruder.restrained() && !intruder.lying)						//	2.	Arrested/Unconcious/Crawling people? - don't care 					(intentional)
			node.intruder_alert(intruder)									// 	3.	All good? report the good guy get his ass!!




// # Below you is a sendPath() proc.
//
// 		# What does it do?
//			It's background thinking. Gameplay-wise it's unseen. To understand where and how this bullshit happens:
// 				1. Go to [KPK.dm]
//				2. press CTRL+F
//				3. enter "#pathfinder" into the field
//
//		---
// 		VARS:
// 		[end] 				during gameplay: it's a node we chose on KPK using UI button called > (press Z, then Find Path)
// 		[already_checked] 	makes sure we dont fucking crash the game because proc is recursive. (Nodes call to each other sendPath proc)
//		[way_to_go] 		is a list to which we add [datum "junctions"]. Contents are passed to proc caller's list: [ihaveplacestobe], the caller is [centor_kpk] --- [KPK.dm]
//


/obj/machinery/node/proc/sendPath(var/obj/machinery/node/end, var/list/already_checked = list(), var/obj/item/centor_kpk/kpk, var/list/way_to_go = list())

	if(src in already_checked)	// Prevent recursiveness of the proc from happening 2+ times by checking the funny list.
		return

	already_checked.Add(src)	// if we didn't sendPath(), make sure it knows we checked it due to [already_checked] list()

//
	if(src == end)						// We are the node to show path to! WE are the destination
		kpk.ihaveplacestobe = way_to_go	// kpk has [ihaveplacestobe] list with invis objs, on top them we draw "holo arrows" (overlay) --- [KPK.dm]
		kpk.node_here = end				// this is for reverse_arrows() proc, we cant lie to user about direction of destination --- [KPK.dm]
		kpk.refresh_overlay()
		return

// Code-wise: We use global [excelsior_junctions] list to form another list full of [excelsior_junction] of from [closest] node to [destination]
// Explained for meatbags:
//	excelsior_junction is a holographic road consisting of arrows
//	every junction created goes into the global list excelsior_junctions with an S at the end!!!
//	Below these comments, NODE, checks global list, asks if its inside any excelsior_junction (which stores 2 nodes, any path in the world has A and B)
//		- WHY? We need a list of JUNCTIONS leading through NODES leading to our DESTINATION
//		- SO: we do it by sending a proc wave through that remembers what nodes it visited, until it meets the DESTINATION

	for(var/datum/excelsior_junction/short_road in excelsior_junctions)

		if(short_road.first == src)
			var/list/transmit_way_to_go = list()
			transmit_way_to_go.Add(way_to_go)
			transmit_way_to_go.Add(short_road)
			short_road.second.sendPath(end, already_checked, kpk, transmit_way_to_go)

		else if(short_road.second == src)
			var/list/transmit_way_to_go = list()
			transmit_way_to_go.Add(way_to_go)
			transmit_way_to_go.Add(short_road)
			short_road.first.sendPath(end, already_checked, kpk, transmit_way_to_go)

// APPENDIX?
/*
*	FUN FACT: "Packaged Node" is inside [machinery_crates.dm]
*/


